PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX ex: <https://w3id.org/sbeo/example/officescenario#>
PREFIX sbeo: <https://w3id.org/sbeo#>
PREFIX seas: <https://w3id.org/seas/>
PREFIX sosa: <http://www.w3.org/ns/sosa/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>

select distinct ?person ?origin (sample(?destination) as ?nextSpace) ?val (sample(?commonEdge) as ?edge)
where {
        ?person a foaf:Person ;
                sbeo:locatedIn ?origin ;
                sbeo:id ?val .

    FILTER (EXISTS {?person sbeo:hasMotionState ex:Standing}) .

        ?origin sbeo:connectedTo ?destination  .
    FILTER (NOT EXISTS {?destination  sbeo:hasAvailabilityStatus  sbeo:UnAvailable}) .

        ?destination  a ?location .
        {?location rdfs:subClassOf* sbeo:RoutePoint }
    UNION
        {?location rdfs:subClassOf* seas:Room }
    UNION
        {?location rdfs:subClassOf* seas:Hall}
            
            
        ?origin sbeo:connectedTo ?commonEdge.    
        ?destination  sbeo:connectedTo ?commonEdge.
        
        ?commonEdge a ?location2 .
        ?location2 rdfs:subClassOf* sbeo:Passage .
    FILTER (NOT EXISTS {?commonEdge  sbeo:hasAvailabilityStatus  sbeo:UnAvailable}) .

      }
group by ?person ?origin ?val
ORDER By ASC(?person)