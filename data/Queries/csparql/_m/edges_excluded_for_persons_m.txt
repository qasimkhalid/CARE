SELECT DISTINCT ?person ?id ?edge ?edgeIDArbitrary
FROM STREAM <https://w3id.org/sbeo/example/officescenario> [RANGE 1s TUMBLING]
FROM <https://w3id.org/sbeo#>
WHERE {
    {
        SELECT DISTINCT (?person1 as ?person) ?edge (sample(?edgeId) as ?edgeIDArbitrary) (?idPerson1  as ?id)
        WHERE {
                ?person1 a foaf:Person ;
                         sbeo:id ?idPerson1 .
                Filter (
                    EXISTS {
                            ?person1 a ?p1 .
                            ?p1 rdfs:subClassOf+ sbeo:MobilityWithWheelchairPerson .
                            FILTER (?p1 != foaf:Person)
                    }
                )
                OPTIONAL {
                            ?edge a ?location ;
                                  sbeo:hasSafetyValue ?safetyVal ;

                                  sbeo:connectedTo ?origin ,
                                                   ?destination .

                            ?origin sbeo:id ?id1 .
                            ?destination sbeo:id ?id2 .
                            FILTER(?origin != ?destination) .
                            FILTER(?id1 != ?id2) .

                            bind(concat(str(?id1) , ", ", str(?id2)) as ?edgeId)

                            {?location rdfs:subClassOf* sbeo:Passage . }
                            FILTER(?safetyVal <= 0.5) .
                }


        }
        GROUP BY ?person1 ?edge ?idPerson1
    }
    UNION
    {
        SELECT DISTINCT (?person2 as ?person) (?idPerson2  as ?id)
        WHERE {
                ?person2 a foaf:Person ;
                        sbeo:id ?idPerson2  .
                Filter (
                    NOT EXISTS {
                                ?person2 a ?p2 .
                                ?p2 rdfs:subClassOf+ sbeo:MobilityWithWheelchairPerson .
                                FILTER (?p2 != foaf:Person)
                    }
                )
        }
    }
}